plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'idea'
}

defaultTasks 'build'
version='2.0.0'
description='Java implementation of the ISO 8583 protocol, focused on making the creation, edition and reading of ISO8583 messages as simple and flexible as possible.'
sourceCompatibility=11
targetCompatibility=11
group='net.sf.j8583'

repositories {
	mavenCentral()
}

configurations {
	deployerJars
	published.extendsFrom archives, signatures
}

dependencies {
	api 'org.slf4j:slf4j-api:1.7.30'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1',
        'org.junit.jupiter:junit-jupiter-params:5.9.1'
	testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.30',
        'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

test {
    useJUnitPlatform()
}

jar {
	manifest {
		attributes "Automatic-Module-Name": "com.solab.j8583"
		attributes "Main-Class": "com.solab.iso8583.util.SimpleParser"
	}
}

tasks.javadoc.options.use=true
tasks.javadoc.options.source='11'
tasks.javadoc.options.links=['https://docs.oracle.com/en/java/javase/11/docs/api/', 'http://www.slf4j.org/apidocs/' ]

task javadocJar(type:Jar, dependsOn:'javadoc') {
	from javadoc.destinationDir
	classifier='javadoc'
}
task sourcesJar(type:Jar) {
	from sourceSets.main.allSource
	classifier='sources'
}

tasks.withType(AbstractCompile) { AbstractCompile compile ->
	compile.options.debug = true
	compile.options.compilerArgs = ['-Xlint:all']
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
	repositories {
        maven {
            name = 'Sonatype'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
                password = project.hasProperty('nexusPassword') ? project.nexusPassword : ''
            }
        }
    }
    publications {
        j8583(MavenPublication) {
            artifactId = 'j8583'
            from components.java
			pom {
				name = 'j8583'
				packaging = 'jar'
				description = project.description
				url = 'http://j8583.sourceforge.net/'
				inceptionYear = '2007'
				scm {
					url = 'https://bitbucket.org/chochos/j8583'
					connection = 'scm:git://bitbucket.org/chochos/j8583.git'
					developerConnection = 'scm:git@bitbucket.org:chochos/j8583.git'
				}
				licenses {
					license {
						name = 'GNU Lesser General Public License, version 3'
						url = 'http://www.gnu.org/licenses/lgpl-3.0.html'
						distribution = 'repo'
					}
				}
				developers {
					developer {
						id = 'chochos'
						name = 'Enrique Zamudio Lopez'
						email = 'chochos@users.sourceforge.net'
						url = 'http://javamexico.org/blogs/ezamudio'
						properties = [
							twitter: 'chochosmx'
						]
						timezone = 'America/Mexico_City'
					}
				}
			}
		}
	}
}

signing {
	required { gradle.taskGraph.hasTask('publish') }
    useGpgCmd()
    sign publishing.publications.j8583
}
